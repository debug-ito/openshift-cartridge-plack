#!/usr/bin/env perl

use strict;
use warnings;
use Daemon::Control;

my $LIB_DIR = "$ENV{OPENSHIFT_PLACK_DIR}/local";
$ENV{PERL_CPANM_OPT} = "--quiet -l '$LIB_DIR'";

close(STDERR);
open(STDERR, ">&STDOUT");

sub standard_control {
    Daemon::Control->new({
        name => $ENV{OPENSHIFT_APP_NAME},
        program => "$LIB_DIR/bin/plackup",
        program_args => ["$ENV{OPENSHIFT_REPO_DIR}/app.psgi", "--port", "$ENV{OPENSHIFT_PLACK_PORT}"],
        ## init_config => "path/to/shell_script/to/set/environment",
        pid_file => "$ENV{OPENSHIFT_PLACK_DIR}/run/plackup.pid",
        fork => 2,
    })->run();
}


my $ENV_FILE = "$ENV{OPENSHIFT_REPO_DIR}/env.pl";
if(-f $ENV_FILE) {
    local $@;
    local $!;
    my $ret = do($ENV_FILE);
    if(!defined($ret)) {
        if($@) {
            print "Error while compiling env.pl: $@\n";
            exit 1;
        }elsif($!) {
            print "Error while reading env.pl: $!\n";
            exit 1;
        }
    }
}

my $action = $ARGV[0];

if(!$action) {
    print "action is not specified\n";
    exit 1;
}

my %action_handler_for = (
    (map { $_ => \&standard_control } qw(start stop restart reload status)),
    build => sub {
        print "-----> Installing dependency\n";
        chdir "$ENV{OPENSHIFT_REPO_DIR}";
        my $result = system("$LIB_DIR/bin/cpanm", "--installdeps", ".");
        if($result != 0) {
            exit $result;
        }
        print "-----> Testing\n";
        $result = system("prove");
        if($result != 0) {
            exit $result;
        }
        print "-----> Done\n";
    }
);

my $handler = $action_handler_for{$action};
if($handler) {
    $handler->();
}else {
    print "No handler for $action\n";
}
exit 0;
